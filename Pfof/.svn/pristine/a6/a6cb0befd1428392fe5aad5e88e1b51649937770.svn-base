Module modreadcube

  Use modconstant
  Use modhdf5
  Use modmpicom
  Use modparameters
  Use modvariables

  Implicit none

Contains

  !=======================================================================
  Subroutine h5readcommonmetadata(file_id)

    Implicit none

    Integer(kind=hid_t), intent(in) :: file_id

    Character(len=H5STRLEN) :: groupname
    Character(len=H5STRLEN) :: aname

    Integer(kind=hid_t) :: gr_id
    Integer(kind=hid_t) :: gr_ramses_id
    Integer(kind=4) :: tmpint4

    groupname = 'metadata'
    Call hdf5_open_group(file_id,groupname, gr_id)

    aname = 'snapshot'
    Call hdf5_read_attr(gr_id, aname, snapshot)

    aname = 'nres'
    Call hdf5_read_attr(gr_id, aname, nres)

    aname = 'potential_bool'
    Call hdf5_read_attr(gr_id, aname, tmpint4)
    If( (tmpint4==0) .and. potential )  Then
       If(procID==0) Then
          Print *,'The potential was not written in the cube files used for this analysis.'
          Print *,'You should change the potential parameter to false.'
       End If
       Call EmergencyStop('Potential missing in the cube files: abort.',100)       
    End If

    ! Write force logical as an integer attribute (1=true, 0=false)
    aname = 'gravitational_field_bool'
    Call hdf5_read_attr(gr_id, aname, tmpint4)
    If( (tmpint4==0) .and. force )  Then
       If(procID==0) Then
          Print *,'The gravitational field was not written in the cube files used for this analysis.'
          Print *,'You should change the force parameter to false.'
       End If
       Call EmergencyStop('Gravitational field missing in the cube files: abort.',100)       
    End If

    ! Ramses Info Metadata
    groupname = 'info_ramses'
    Call hdf5_open_group(gr_id,groupname,gr_ramses_id)

    aname = 'nproc'
    Call hdf5_read_attr(gr_ramses_id,aname,nproc)

    aname = 'ndim'
    Call hdf5_read_attr(gr_ramses_id,aname,ndim)

    aname = 'lmin'
    Call hdf5_read_attr(gr_ramses_id,aname,lmin)

    aname = 'lmax'
    Call hdf5_read_attr(gr_ramses_id,aname,lmax)

    aname = 'ngridmax'
    Call hdf5_read_attr(gr_ramses_id,aname,ngridmax)

    aname = 'nstep_coarse'
    Call hdf5_read_attr(gr_ramses_id,aname,nstep_coarse)

    aname = 'boxlen'
    Call hdf5_read_attr(gr_ramses_id,aname,boxlen)

    aname = 'time'
    Call hdf5_read_attr(gr_ramses_id,aname,time)

    aname = 'aexp'
    Call hdf5_read_attr(gr_ramses_id,aname,aexp)

    aname = 'h0'
    Call hdf5_read_attr(gr_ramses_id,aname,h0)

    aname = 'omega_m'
    Call hdf5_read_attr(gr_ramses_id,aname,omega_m)
    
    aname = 'omega_l'
    Call hdf5_read_attr(gr_ramses_id,aname,omega_l)

    aname = 'omega_k'
    Call hdf5_read_attr(gr_ramses_id,aname,omega_k)

    aname = 'omega_b'
    Call hdf5_read_attr(gr_ramses_id,aname,omega_b)

    aname = 'unit_l'
    Call hdf5_read_attr(gr_ramses_id,aname,unit_l)

    aname = 'unit_d'
    Call hdf5_read_attr(gr_ramses_id,aname,unit_d)

    aname = 'unit_t'
    Call hdf5_read_attr(gr_ramses_id,aname,unit_t)

    Call hdf5_close_group(gr_ramses_id)

    Call hdf5_close_group(gr_id)

  End Subroutine h5readcommonmetadata


  !=======================================================================
  !> This subroutine reads hdf5 cube files created by pFOF.
  Subroutine h5readcube()

    Implicit none

    Character(len=400) :: filename                          ! File name
    Integer(hid_t) :: file_id                               ! File identifier
    Integer(hid_t) :: gr_id                                 ! Group identifier
    Character(len=5)  :: pid_char
    Character(len=H5STRLEN) :: aname                              ! Attribute name
    Character(len=H5STRLEN) :: dsetname                           ! Dataset name
    Character(len=H5STRLEN) :: groupname
    Real(kind=4), dimension(6) :: boundaries
    Integer(kind=8) :: npart8

#ifdef DEBUG
    Print *,"Enter h5readcube on process ",procID
#endif

    Write(pid_char(1:5),'(I5.5)') procID
    filename = trim(output_root)//'_cube_'//pid_char//'.h5'

    ! open the file
    Call hdf5_open_file(filename, file_id)

    Call h5readcommonmetadata(file_id)

    ngrid = int(nres,kind=8)**3
    npart = ngrid

    ! open the root group
    groupname = 'metadata'
    Call hdf5_open_group(file_id,groupname, gr_id)

    ! read attribute partNB
    dsetname = 'npart_file'
    Call hdf5_read_data(gr_id, dsetname, npart8)
    mynpart = int(npart8, kind=4)

    ! read attribute boundaries
    aname = 'boundaries'
    Call hdf5_read_attr(gr_id, aname, 6, boundaries)

    xmin=boundaries(1)
    xmax=boundaries(2)
    ymin=boundaries(3)
    ymax=boundaries(4)
    zmin=boundaries(5)
    zmax=boundaries(6)

    Call hdf5_close_group(gr_id)

    If(.not.Allocated(id)) Allocate(id(mynpart))
    If(.not.Allocated(pos))  Allocate(pos(3,mynpart))
    If(.not.Allocated(vel))  Allocate(vel(3,mynpart))

    groupname = 'data'
    Call hdf5_open_group(file_id, groupname, gr_id)

    ! read position of the particles
    dsetname = 'position_part'
    Call hdf5_read_data(gr_id, dsetname, 3, mynpart, pos)

    ! read velocity of the particles
    dsetname = 'velocity_part'
    Call hdf5_read_data(gr_id, dsetname, 3, mynpart, vel)

    ! read id of the particles
    dsetname = 'identity_part'
    Call hdf5_read_data(gr_id, dsetname, mynpart, id)
    
    If(potential) Then
       If(.not.Allocated(pot)) Allocate(pot(mynpart))
       dsetname = 'potential_part'
       Call hdf5_read_data(gr_id, dsetname, mynpart, pot)
    End If

    If(force) Then
       If(.not.Allocated(for)) Allocate(for(3,mynpart))
       dsetname = 'gravitational_field_part'
       Call hdf5_read_data(gr_id, dsetname, 3, mynpart, for)
    End If

    ! Close the root group.
    Call hdf5_close_group(gr_id)

    Call hdf5_close_file(file_id)


#ifdef DEBUG
    Print *,"Exit h5readcube on process ",procID
#endif

  End Subroutine h5readcube



  !=======================================================================
  !> This subroutine reads hdf5 sorted cube files created by pFOF.
  Subroutine h5readsortedcube()

    Implicit none

    Character(len=400) :: filename                          ! File name
    Integer(hid_t) :: file_id                               ! File identifier
    Integer(hid_t) :: gr_data_id                            ! Group identifier
    Integer(hid_t) :: gr_id                                 ! Group identifier
    Character(len=5)  :: pid_char
    Character(len=8)  :: gid_char
    Character(len=H5STRLEN) :: aname                              ! Attribute name
    Character(len=H5STRLEN) :: dsetname                           ! Dataset name
    Character(len=H5STRLEN) :: groupname
    Real(kind=4), dimension(6) :: boundaries
    Integer(kind=4) :: potattr
    Integer(kind=4) :: forattr
    Integer(kind=4) :: ngroup
    Integer(kind=4), dimension(:), allocatable :: npartpergroup
    Integer(kind=4) :: igroup
    Integer(kind=4) :: indbeg, indend

    Integer(kind=8) :: npart8

#ifdef DEBUG
    Print *,"Enter h5readsortedcube on process ",procID
#endif

    Write(pid_char(1:5),'(I5.5)') procID
    filename = trim(output_root)//'_sortedcube_'//pid_char//'.h5'

    ! open the file
    Call hdf5_open_file(filename, file_id)

    Call h5readcommonmetadata(file_id)

    ngrid = int(nres,kind=8)**3
    npart = ngrid

    ! open the root group
    groupname = 'metadata'
    Call hdf5_open_group(file_id,groupname, gr_id)

    ! read attribute partNB
    dsetname = 'npart_file'
    Call hdf5_read_data(gr_id, dsetname, npart8)
    mynpart = int(npart8,kind=4)

    ! read attribute boundaries
    aname = 'boundaries'
    Call hdf5_read_attr(gr_id, aname, 6, boundaries)

    xmin=boundaries(1)
    xmax=boundaries(2)
    ymin=boundaries(3)
    ymax=boundaries(4)
    zmin=boundaries(5)
    zmax=boundaries(6)

    ! read attribute ngroup
    aname = 'ngroup'
    Call hdf5_read_attr(gr_id, aname, ngroup)

    Allocate(npartpergroup(ngroup))
    aname = 'npart_grp_array'
    Call hdf5_read_data(gr_id, aname, ngroup, npartpergroup)

    Call hdf5_close_group(gr_id)

    groupname='data'
    Call hdf5_open_group(file_id, groupname, gr_data_id)

    If(.not.Allocated(id)) Allocate(id(mynpart))
    If(.not.Allocated(pos))  Allocate(pos(3,mynpart))
    If(.not.Allocated(vel))  Allocate(vel(3,mynpart))

    indbeg = 1
    indend = 0
    ! loop over the groups
    Do igroup=1, ngroup
       If(npartpergroup(igroup) /= 0) Then ! there is at least one part. to read in this group
          Write(gid_char(1:8),'(I8.8)') igroup
          groupname='group'//gid_char
          Call hdf5_open_group(gr_data_id,groupname,gr_id)
          
          indend = indbeg + npartpergroup(igroup) - 1
          ! read position of the particles
          dsetname = 'position_part'
          Call hdf5_read_data(gr_id, dsetname, 3, mynpart, pos(:,indbeg:indend))
          
          ! read velocity of the particles
          dsetname = 'velocity_part'
          Call hdf5_read_data(gr_id, dsetname, 3, mynpart, vel(:,indbeg:indend))
          
          ! read id of the particles
          dsetname = 'identity_part'
          Call hdf5_read_data(gr_id, dsetname, mynpart, id(indbeg:indend))

          ! read potential if requested
          If(potential) Then
             If(.not.Allocated(pot)) Allocate(pot(mynpart))
             dsetname = 'potential_part'
             Call hdf5_read_data(gr_id, dsetname, mynpart, pot(indbeg:indend))
          End If

          ! read force if requested
          If(force) Then
             If(.not.Allocated(for)) Allocate(for(3,mynpart))
             dsetname = 'gravitational_field_part'
             Call hdf5_read_data(gr_id, dsetname, 3, mynpart, for(:,indbeg:indend))
          End If
          
          indbeg = indend + 1

          Call hdf5_close_group(gr_id)
       End If
    End Do

    If(indend /= mynpart) Then
       Print *,'Error while reading particles from file ',filename
       Call EmergencyStop('Error in h5readsortedcube',100)
    End If

    ! Close the root group.
    Call hdf5_close_group(gr_data_id)

    Call hdf5_close_file(file_id)

    Deallocate(npartpergroup)

#ifdef DEBUG
    Print *,"Exit h5readsortedcube on process ",procID
#endif

  End Subroutine h5readsortedcube


  !=======================================================================
 
  Subroutine mpih5readcube()
    
    Implicit none

    Character(len=400) :: filecube
    Character(len=5)  :: pid_char

    Character(len=H5STRLEN) :: dsetname                           ! Dataset name
    Character(len=H5STRLEN) :: aname                              ! Attribute name
    Character(len=H5STRLEN) :: groupname

    Integer(hid_t) :: file_id                               ! File identifier
    Integer(hid_t) :: gr_id                                 ! Group identifier
    
    Integer(kind=4) :: procperfile
    Integer(kind=4), dimension(:), allocatable :: partnb_tab
    Integer(kind=4) :: potattr
    Integer(kind=4) :: forattr

    Real(kind=4), dimension(:,:), allocatable :: bound_tab
    Integer(kind=8) :: npart8
    
#ifdef DEBUG
    Print *,"Enter mpih5readcube on process ",procID
#endif    
    
    ! number of processes writing in the same file
    procperfile = gatherread**3
    
    Allocate(partnb_tab(procperfile))
    Allocate(bound_tab(6,procperfile))
        
    Write(pid_char(1:5),'(I5.5)') commcolorRead
    filecube = trim(output_root)//'_mpicube_'//pid_char//'.h5'

    Call hdf5_open_mpi_file(filecube, MpiSubCubeRead, file_id)

    Call h5readcommonmetadata(file_id)

    ngrid = int(nres,kind=8)**3
    npart = ngrid

    ! open the root group
    groupname = 'metadata'
    Call hdf5_open_group(file_id,groupname, gr_id)

    ! read attribute partNB
    dsetname = 'npart_file'
    Call hdf5_read_data(gr_id, dsetname, npart8)
    mynpart = int(npart8,kind=4)

    ! read attribute partNB
    aname = 'npart_cube_array'
    Call hdf5_read_attr(gr_id, aname, 6, partnb_tab)

#ifdef DEBUG
    Print *,"partNB_tab read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    mynpart = partnb_tab(scprocIDRead+1)

    ! read attribute boundaries
    aname = 'boundaries_array'
    Call hdf5_read_attr(gr_id, aname, 6, procperfile, bound_tab)

#ifdef DEBUG
    Print *,"boundaries_tab read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    xmin=bound_tab(1,scprocIDRead+1)
    xmax=bound_tab(2,scprocIDRead+1)
    ymin=bound_tab(3,scprocIDRead+1)
    ymax=bound_tab(4,scprocIDRead+1)
    zmin=bound_tab(5,scprocIDRead+1)
    zmax=bound_tab(6,scprocIDRead+1)

    Call hdf5_close_group(gr_id)

    If(.not.Allocated(id)) Allocate(id(mynpart))
    If(.not.Allocated(pos))  Allocate(pos(3,mynpart))
    If(.not.Allocated(vel))  Allocate(vel(3,mynpart))

    groupname = 'data'
    Call hdf5_open_group(file_id, groupname, gr_id)

    dsetname = 'position_part'
    Call hdf5_read_mpi_data(gr_id, dsetname, 3, mynpart, pos, MpiSubCubeRead)

#ifdef DEBUG
    Print *,"pos read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    dsetname = 'velocity_part'
    Call hdf5_read_mpi_data(gr_id, dsetname, 3, mynpart, vel, MpiSubCubeRead)

#ifdef DEBUG
    Print *,"vel read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    dsetname = 'identity_part'
    Call hdf5_read_mpi_data(gr_id, dsetname, mynpart, id, MpiSubCubeRead)

#ifdef DEBUG
    Print *,"ID read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    If(potential) Then
       If(.not.Allocated(pot)) Allocate(pot(mynpart))
       dsetname = 'potential_part'
       Call hdf5_read_mpi_data(gr_id, dsetname, mynpart, pot, MpiSubCubeRead)
    End If

    ! if requested, try to read the force attribute
    If(force) Then
       If(.not.Allocated(for)) Allocate(for(3,mynpart))
       dsetname = 'gravitational_field_part'
       Call hdf5_read_mpi_data(gr_id, dsetname, 3, mynpart, for, MpiSubCubeRead)
    End If

    ! Close the root group.
    Call hdf5_close_group(gr_id)

    Call hdf5_close_file(file_id)
    
    Deallocate(partnb_tab)
    Deallocate(bound_tab)


#ifdef DEBUG
    Print *,"Exit mpih5readcube on process ",procID
#endif    
    
    
  End Subroutine mpih5readcube


  !=======================================================================
  Subroutine mpih5readsortedcube()
    
    Implicit none

    Character(len=400) :: filecube
    Character(len=5)  :: pid_char
    Character(len=8)  :: gid_char

    Character(len=H5STRLEN) :: dsetname                           ! Dataset name
    Character(len=H5STRLEN) :: aname                              ! Attribute name
    Character(len=H5STRLEN) :: groupname

    Integer(hid_t) :: file_id                               ! File identifier
    Integer(hid_t) :: gr_data_id                            ! Group identifier
    Integer(hid_t) :: gr_id                                 ! Group identifier
    
    Integer(kind=4) :: procperfile
    Integer(kind=4), dimension(:), allocatable :: partnb_tab
    Integer(kind=4) :: potattr
    Integer(kind=4) :: forattr
    Integer(kind=4) :: igroup, firstgroup, lastgroup
    Integer(kind=4) :: indbeg, indend
    Integer(kind=4) :: nc, ngroup
    
    Integer(kind=4), dimension(:), allocatable :: npartpergroup

    Real(kind=4), dimension(:,:), allocatable :: bound_tab
    Integer(kind=8) :: npart8
    
#ifdef DEBUG
    Print *,"Enter mpih5readsortedcube on process ",procID
#endif    
    
    ! number of processes reading in the same file
    procperfile = gatherread**3
    
    Allocate(partnb_tab(procperfile))
    Allocate(bound_tab(6,procperfile))
        
    Write(pid_char(1:5),'(I5.5)') commcolorRead
    filecube = trim(output_root)//'_mpisortedcube_'//pid_char//'.h5'

    ! we open the file for a serial read
    Call hdf5_open_file(filecube, file_id)

    Call h5readcommonmetadata(file_id)

    ngrid = int(nres,kind=8)**3
    npart = ngrid

    ! open the root group
    groupname = 'metadata'
    Call hdf5_open_group(file_id,groupname, gr_id)

    ! read attribute partNB
    dsetname = 'npart_file'
    Call hdf5_read_data(gr_id, dsetname, npart8)
    mynpart = int(npart8,kind=4)

#ifdef DEBUG
    Print *,"Value of nres read = ",nres, " on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif

    ! read attribute partNB
    aname = 'npart_cube_array'
    Call hdf5_read_attr(gr_id, aname, procperfile, partnb_tab)

#ifdef DEBUG
    Print *,"partNB_tab read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    mynpart = partnb_tab(scprocIDRead+1)

    ! read attribute boundaries
    aname = 'boundaries_array'
    Call hdf5_read_attr(gr_id, aname, 6, procperfile, bound_tab)

#ifdef DEBUG
    Print *,"boundaries_tab read on process ",procID
    Call Mpi_Barrier(Mpi_Comm_World, mpierr)
#endif    

    xmin=bound_tab(1,scprocIDRead+1)
    xmax=bound_tab(2,scprocIDRead+1)
    ymin=bound_tab(3,scprocIDRead+1)
    ymax=bound_tab(4,scprocIDRead+1)
    zmin=bound_tab(5,scprocIDRead+1)
    zmax=bound_tab(6,scprocIDRead+1)

    ! read attribute ngroup
    aname = 'ngroup'
    Call hdf5_read_attr(gr_id, aname, ngroup)

    Allocate(npartpergroup(ngroup))
    aname = 'npart_grp_array'
    Call hdf5_read_data(gr_id, aname, ngroup, npartpergroup)

    nc = ngroup / procperfile
    firstgroup = nc * scprocIDRead + 1
    lastgroup  = nc *(scprocIDRead+1)

    Call hdf5_close_group(gr_id)

    If(.not.Allocated(id)) Allocate(id(mynpart))
    If(.not.Allocated(pos))  Allocate(pos(3,mynpart))
    If(.not.Allocated(vel))  Allocate(vel(3,mynpart))

    groupname = 'data'
    Call hdf5_open_group(file_id, groupname, gr_data_id)

    indbeg = 1
    indend = 0
    ! loop over the groups
    Do igroup=firstgroup, lastgroup
       If(npartpergroup(igroup) /= 0) Then ! there is at least one part. to read in this group
          Write(gid_char(1:8),'(I8.8)') igroup
          groupname='group'//gid_char
          Call hdf5_open_group(gr_data_id,groupname,gr_id)
          
          indend = indbeg + npartpergroup(igroup) - 1
          ! read position of the particles
          dsetname = 'position_part'
          Call hdf5_read_data(gr_id, dsetname, 3, mynpart, pos(:,indbeg:indend))
          
          ! read velocity of the particles
          dsetname = 'velocity_part'
          Call hdf5_read_data(gr_id, dsetname, 3, mynpart, vel(:,indbeg:indend))
          
          ! read id of the particles
          dsetname = 'identity_part'
          Call hdf5_read_data(gr_id, dsetname, mynpart, id(indbeg:indend))
          
          ! read potential if requested
          If(potential) Then
             If(.not.Allocated(pot)) Allocate(pot(mynpart))
             dsetname = 'potential_part'
             Call hdf5_read_data(gr_id, dsetname, mynpart, pot(indbeg:indend))
          End If

          ! read force if requested
          If(force) Then
             If(.not.Allocated(for)) Allocate(for(3,mynpart))
             dsetname = 'gravitational_field_part'
             Call hdf5_read_data(gr_id, dsetname, 3, mynpart, for(:,indbeg:indend))
          End If
          
          indbeg = indend + 1

          Call hdf5_close_group(gr_id)
       End If
    End Do


    ! Close the root group.
    Call hdf5_close_group(gr_data_id)

    Call hdf5_close_file(file_id)
    
    Deallocate(partnb_tab)
    Deallocate(bound_tab)

#ifdef DEBUG
    Print *,"Exit mpih5readsortedcube on process ",procID
#endif    
        
  End Subroutine mpih5readsortedcube

End Module modreadcube
